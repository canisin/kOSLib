@LAZYGLOBAL OFF.
RUN ONCE REQUIRE.

REQUIRE("bud").
REQUIRE("steering").

LOCAL tHeading IS -90.
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
LOCK STEERING TO HEADING(tHeading, 90).
LOCK THROTTLE TO 1.

PRINT "Begin countdown..".
WAIT 5.

PRINT "Launch!".
STAGE.
WAIT UNTIL ALTITUDE > 500.

PRINT "Tipping over..".
LOCK STEERING TO HEADING(tHeading, VPITCH(SRFPROGRADE) - 10).
WAIT UNTIL VPITCH(SRFPROGRADE) < 50.

PRINT "Holding pitch..".
LOCK STEERING TO HEADING(tHeading, 45).
WAIT UNTIL AVAILABLETHRUST = 0.
UNLOCK THROTTLE.

PRINT "Dropping booster..".
STAGE.
WAIT 2.

PRINT "Igniting main engine..".
STAGE.
WAIT 2.

PRINT "Launch sequence complete.".
UNLOCK STEERING.

PRINT "Awaiting end of vertical ascent..".
WAIT UNTIL VERTICALSPEED < 0.
PRINT "Descent detected.".

PRINT "Killing horizontal velocity..".
LOCK STEERING TO NOROT(-VXCL(UP:VECTOR, SRFPROGRADE:VECTOR)).
WAITSTEERING(10).
LOCK THROTTLE TO SIGMOID(GROUNDSPEED-5, 10*AVAILACC()).
WAIT UNTIL GROUNDSPEED < 5.
UNLOCK THROTTLE.

PRINT "Reducing vertical speed..".
LOCK STEERING TO SRFRETROGRADE.
WAITSTEERING(5).
LOCK THROTTLE TO SIGMOID(-VERTICALSPEED-10, 3*AVAILACC()).
WAIT UNTIL VERTICALSPEED > -10.
UNLOCK THROTTLE.

PRINT "Awaiting 1500m..".
WAIT UNTIL ALT:RADAR < 1500.
GEAR ON.
PRINT "Reducing vertical speed to 10m/s..".
LOCK THROTTLE TO SIGMOID(-VERTICALSPEED-10, 3*AVAILACC()).
WAIT UNTIL VERTICALSPEED > -10.
UNLOCK THROTTLE.

PRINT "Awaiting 200m..".
WAIT UNTIL ALT:RADAR < 200.
PRINT "Locking vertical speed to 10m/s..".
LOCK THROTTLE TO SIGMOID(-VERTICALSPEED-10, 2*AVAILACC()).

PRINT "Awaiting 100m..".
WAIT UNTIL ALT:RADAR < 100.
PRINT "Locking vertical speed to 5m/s..".
LOCK THROTTLE TO SIGMOID(-VERTICALSPEED-5, 4*AVAILACC()).
WAIT UNTIL ALT:RADAR < 20.

PRINT "Locking vertical speed to 2m/s..".
LOCK THROTTLE TO SIGMOID(-VERTICALSPEED-2, 8*AVAILACC()).
WAIT UNTIL STATUS = "LANDED" OR STATUS = "SPLASHED".

PRINT "Landed.".
UNLOCK THROTTLE.
UNLOCK STEERING.
