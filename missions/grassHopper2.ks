@LAZYGLOBAL OFF.
RUN ONCE REQUIRE.

REQUIRE("bud").
REQUIRE("steering").
REQUIRE("burnTime").
REQUIRE("steeringDebug").
_debugSteeringToggle ON.

LOCAL tHeading IS -90.
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
LOCK STEERING TO HEADING(tHeading, 90).
LOCK THROTTLE TO 1.

PRINT "Begin countdown..".
WAIT 5.

PRINT "Launch!".
STAGE.
WAIT UNTIL ALTITUDE > 500.

PRINT "Tipping over..".
LOCK STEERING TO HEADING(tHeading, VPITCH(SRFPROGRADE) - 10).
WAIT UNTIL VPITCH(SRFPROGRADE) < 50.

PRINT "Holding pitch..".
LOCK STEERING TO HEADING(tHeading, 45).
WAIT UNTIL AVAILABLETHRUST = 0.
UNLOCK THROTTLE.

PRINT "Dropping booster..".
STAGE.
WAIT 2.

PRINT "Igniting main engine..".
STAGE.
WAIT 2.

PRINT "Launch sequence complete.".
UNLOCK STEERING.

PRINT "Awaiting end of vertical ascent..".
WAIT UNTIL VERTICALSPEED < 0.

PRINT "Killing horizontal velocity..".
LOCK STEERING TO NOROT(-VXCL(UP:VECTOR, SRFPROGRADE:VECTOR)).
WAITSTEERING(10).
LOCK THROTTLE TO SIGMOID(GROUNDSPEED-5, 10*AVAILACC()).
WAIT UNTIL GROUNDSPEED < 5.
UNLOCK THROTTLE.

PRINT "Pointing upwards..".
//LOCK STEERING TO SRFRETROGRADE.
LOCK STEERING TO HEADING(VHEADING(SRFRETROGRADE),
                       MAX(VPITCH(SRFRETROGRADE), 45)).

PRINT "Awaiting suicide burn..".
LOCK burnAcc TO AVAILACC() - CONSTANT:G.
LOCK burnHeight TO VERTICALSPEED^2 / (2*burnAcc).
WAIT UNTIL ALT:RADAR <= burnHeight + 250.

PRINT "Suicide burn!".
LOCK THROTTLE TO SIGMOID(-VERTICALSPEED-10, 2*AVAILACC()).
GEAR ON.

PRINT "Awaiting 100m..".
WAIT UNTIL ALT:RADAR < 100.

PRINT "Locking vertical speed to 5m/s..".
LOCK THROTTLE TO SIGMOID(-VERTICALSPEED-5, 4*AVAILACC()).

PRINT "Awaiting 20m..".
WAIT UNTIL ALT:RADAR < 20.

PRINT "Locking vertical speed to 2m/s..".
LOCK THROTTLE TO SIGMOID(-VERTICALSPEED-2, 8*AVAILACC()).
WAIT UNTIL STATUS = "LANDED" OR STATUS = "SPLASHED".

PRINT "Landed.".
UNLOCK THROTTLE.
UNLOCK STEERING.
